{
  description = "zhyie's Nix Flake Configuration";

  nixConfig = {
    extra-substituters = [ "https://noctalia.cachix.org" ];
    extra-trusted-public-keys = [
      "noctalia.cachix.org-1:pCOR47nnMEo5thcxNDtzWpOxNFQsBRglJzxWPp3dkU4="
    ];
  };

  inputs = {
    # self.submodules = true;

    #: NIXOS AND NIXPKGS ----------------------------------------
    nixpkgs.follows = "nixos-stable";
    nixpkgs-droid.follows = "nixpkgs";
    nixos-unstable.url = "github:NixOS/nixpkgs/nixos-unstable";
    nixos-stable.url = "github:NixOS/nixpkgs/nixos-26.05";
    nix-on-droid = {
      url = "github:nix-community/nix-on-droid/release-24.05";
      inputs.nixpkgs.follows = "nixpkgs-droid";
      inputs.home-manager.follows = "home-manager-droid";
      inputs.nix-formatter-pack.inputs.nmd.follows = "nix-on-droid/nmd";
    };

    #: HOME MANAGER ---------------------------------------------
    home-manager.follows = "home-manager-stable";
    home-manager-droid.follows = "home-manager";
    home-manager-master = {
      url = "github:nix-community/home-manager";
      inputs.nixpkgs.follows = "nixos-unstable";
    };
    home-manager-stable = {
      url = "github:nix-community/home-manager/release-26.05";
      inputs.nixpkgs.follows = "nixpkgs";
    };

    #: HARDWARE AND SECURITY ------------------------------------
    nixos-hardware.url = "github:NixOS/nixos-hardware/master";
    # lanzaboote = {
    #   url = "github:nix-community/lanzaboote/v1.0.0";
    #   inputs.nixpkgs.follows = "nixpkgs";
    # };
    sops-nix = {
      url = "github:Mic92/sops-nix";
      inputs.nixpkgs.follows = "nixpkgs";
    };

    #: DEVELOPMENT ----------------------------------------------
    direnv-instant = {
      url = "github:Mic92/direnv-instant";
      inputs.nixpkgs.follows = "nixpkgs";
    };
    git-hooks = {
      url = "github:cachix/git-hooks.nix";
      inputs.nixpkgs.follows = "nixpkgs";
    };

    #: MISC -----------------------------------------------------
    nix-flatpak.url = "github:gmodena/nix-flatpak/?ref=latest";
    noctalia = {
      url = "github:noctalia-dev/noctalia-shell";
      inputs.nixpkgs.follows = "nixos-unstable";
      inputs.noctalia-qs.inputs.treefmt-nix.follows = "direnv-instant/treefmt-nix";
    };
    catppuccin = {
      url = "github:catppuccin/nix/release-26.05";
      inputs.nixpkgs.follows = "nixpkgs";
    };

    #: ZHYIE's --------------------------------------------------
    secrets = {
      url = "git+ssh://git@github.com/zhyie/nix-secrets.git?ref=main&shallow=1";
      flake = false;
    };
    dotfiles = {
      url = "github:zrhive/dotfiles/main";
      flake = false;
    };
    suckless = {
      url = "github:zrhive/suckless/main";
      inputs.nixpkgs.follows = "nixpkgs";
    };
  };

  outputs =
    { self, ... }@inputs:
    let
      inherit (inputs.nixpkgs.lib) mapAttrs listToAttrs;

      #: HOSTS AND THEIR SPECIFICATIONS
      hosts = import ./hosts;
      users = import ./users;
      modules = import ./modules;

      #: SET OF ARGS TO PASS IN
      args = {
        inherit
          inputs
          hosts
          users
          modules
          ;
      };
    in
    rec {
      #: FUNCTION LIBRARY
      lib = import ./lib args;

      #: GENERATE HOSTS|HOMES CONFIGURATION
      nixosConfigurations = mapAttrs lib.mkNixos (lib.filterNixos hosts);
      nixOnDroidConfigurations = mapAttrs lib.mkDroid (lib.filterDroid hosts);
      # darwinConfigurations = mapAttrs lib.mkDarwin (lib.filterDarwin hosts);
      homeConfigurations = listToAttrs (lib.mkHome hosts);

      #: EXPORT MODULES
      nixosModules = import ./modules/nixos;
      homeModules = import ./modules/home;

      #: PACKAGES AND OVERLAYS
      overlays = import ./overlays { inherit inputs; };
      packages = lib.eachSystem (pkgs: import ./packages { inherit pkgs inputs; });

      #: FORMATTER
      formatter = lib.eachSystem (pkgs: packages.${pkgs.stdenv.hostPlatform.system}.git-hooks);

      #: SHELL ENVIRONMENT
      devShells = lib.eachSystem (pkgs: {
        default = import ./shell.nix { inherit self pkgs; };
      });

      #: CHECKS
      checks = lib.eachSystem (
        pkgs:
        mapAttrs (h: _: nixosConfigurations.${h}.config.system.build.toplevel) nixosConfigurations
        // mapAttrs (h: _: homeConfigurations.${h}.activationPackage) homeConfigurations
        // {
          git-hooks = import ./hooks.nix { inherit inputs pkgs; };
        }
      );
    };
}
